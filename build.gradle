buildscript {
    configurations.maybeCreate("pitest")
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6'
        pitest 'org.pitest:pitest-junit5-plugin:0.11'
    }
}

plugins {
    id "java"
    id "jacoco"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.sonarqube" version "2.8"
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "info.solidsoft.pitest"

ext {
    springBootVersion = '2.2.2.RELEASE'
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
    testIntegration {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            srcDir file('src/testIntegration/java')
        }
    }
}

configurations {
    testIntegrationImplementation.extendsFrom implementation, testImplementation
    testIntegrationRuntime.extendsFrom runtimeOnly
}

repositories {
    mavenCentral()
}

group = "com.github.mikesafonov"
version "1.0.0"

jar {
    enabled = true
}

dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter")
    api("org.restcomm.smpp:ch-commons-util:7.0.6")
    api("org.restcomm.smpp:ch-commons-charset:7.0.6")
    api("org.restcomm.smpp:ch-commons-gsm:7.0.6")
    api("org.restcomm.smpp:ch-smpp:5.1.0-113")

    implementation("javax.validation:validation-api:2.0.1.Final")

    compileOnly("org.projectlombok:lombok:1.18.10")
    annotationProcessor("org.projectlombok:lombok:1.18.10")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testCompileOnly("org.projectlombok:lombok:1.18.10")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.10")

    testImplementation("org.assertj:assertj-core:3.15.0")

    testImplementation("com.devskiller:jfairy:0.6.2")
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    testIntegrationCompileOnly("org.projectlombok:lombok:1.18.10")
    testIntegrationAnnotationProcessor("org.projectlombok:lombok:1.18.10")
    testIntegrationImplementation('org.springframework.boot:spring-boot-starter-test')
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

javadoc {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

jacoco{
    toolVersion = "0.8.3"
}

jacocoTestReport{
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task testIntegration(type: Test){
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

pitest {
    testPlugin = "junit5"
    targetClasses = ['com.github.mikesafonov.smpp.*']
    excludedClasses = ['com.github.mikesafonov.smpp.config.SmppAutoConfiguration']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}


// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            // Add required metadata to POM
            pom.project {
                name "spring-boot-starter-smpp"
                packaging "jar"
                description "Spring Boot starter for building specifications in declarative way"
                url "https://github.com/MikeSafonov/spring-boot-starter-smpp"
                organization {
                    name "com.github.mikesafonov"
                    url "https://github.com/MikeSafonov"
                }
                issueManagement {
                    system "GitHub"
                    url "https://github.com/MikeSafonov/spring-boot-starter-smpp/issues"
                }
                licenses {
                    license {
                        name "MIT"
                        url "https://github.com/MikeSafonov/spring-boot-starter-smpp/blob/master/LICENSE"
                        distribution "repo"
                    }
                }
                scm {
                    url "https://github.com/MikeSafonov/spring-boot-starter-smpp"
                    connection "scm:git:git://github.com/MikeSafonov/spring-boot-starter-smpp.git"
                    developerConnection "scm:git:ssh://git@github.com:MikeSafonov/spring-boot-starter-smpp.git"
                }
                developers {
                    developer {
                        name "Mike Safonov"
                        organization "com.github.mikesafonov"
                        organizationUrl "https://github.com/MikeSafonov"
                    }
                }
            }
        }
    }
}
