buildscript {
    configurations.maybeCreate("pitest")
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
        pitest 'org.pitest:pitest-junit5-plugin:0.9'
    }
}

plugins {
    id "idea"
    id "java"
    id "jacoco"
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "com.gradle.build-scan" version "2.2"
    id "org.sonarqube" version "2.7"
}

apply plugin: 'java-library'
apply plugin: "info.solidsoft.pitest"

ext {
    springBootVersion = '2.1.6.RELEASE'
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

wrapper {
    gradleVersion = "5.2.1"
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
}

group = "com.github.mikesafonov"

dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    dependencies {
        dependencySet(group:'org.junit.jupiter', version: '5.5.0') {
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-engine'
            entry 'junit-jupiter-params'
        }
        dependencySet(group:'org.junit.platform', version: '1.5.0') {
            entry 'junit-platform-engine'
            entry 'junit-platform-commons'
        }
    }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter")
    api("org.restcomm.smpp:ch-commons-util:7.0.6")
    api("org.restcomm.smpp:ch-commons-charset:7.0.6")
    api("org.restcomm.smpp:ch-commons-gsm:7.0.6")
    api("org.restcomm.smpp:ch-smpp:5.1.0-113")

    implementation("javax.validation:validation-api:2.0.1.Final")

    compileOnly("org.projectlombok:lombok:1.18.4")
    annotationProcessor("org.projectlombok:lombok:1.18.4")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testCompileOnly("org.projectlombok:lombok:1.18.4")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.4")

    testImplementation("org.assertj:assertj-core:3.12.1")

    testImplementation("org.junit.jupiter:junit-jupiter:5.5.0")
    testImplementation("com.devskiller:jfairy:0.6.2")
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }
}

jacoco{
    toolVersion = "0.8.3"
}

jacocoTestReport{
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

test {
    ignoreFailures = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport, "pitest"
}

//buildScan {
//    termsOfServiceUrl = "https://gradle.com/terms-of-service"
//    termsOfServiceAgree = "yes"
//
//    publishAlways()
//}

pitest {
    testPlugin = "junit5"
    targetClasses = ['com.github.mikesafonov.smpp.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}